plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'jacoco'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.5.0'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.5.0'
    implementation 'jakarta.mail:jakarta.mail-api:2.0.0'
    implementation 'com.sun.mail:jakarta.mail:2.0.0'
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"

        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true
        showStandardStreams = false
    }
    finalizedBy jacocoTestReport // report is always generated after tests run

}
task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

application {
    mainClassName = "mystars.Main"
}

shadowJar {
    archiveBaseName = "main"
    archiveClassifier = null
}

checkstyle {
    toolVersion = '8.23'
}

run{
    standardInput = System.in
    enableAssertions = true
}
